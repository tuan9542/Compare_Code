SET QUOTED_IDENTIFIER ON;
SET ANSI_NULLS ON;
GO
ALTER PROC sp_CALLLOGPHAR_WEB_ChangeStep_15 ( @Data NVARCHAR(MAX) = '' )
AS
BEGIN
    DECLARE @StoreName NVARCHAR(50) = OBJECT_NAME(@@PROCID);
    DECLARE @log NVARCHAR(MAX) = CONCAT(@StoreName, ' @Data=''', @Data, '''');
    INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
    VALUES (
               @StoreName -- Title - nvarchar(300)
             , @log -- Error - nvarchar(max)
             , 0 -- Status - tinyint
             , GETDATE() -- TimeCreate - datetime
           );
    DECLARE
        @ResultID          INT           = 0
      , @Message           NVARCHAR(MAX) = N''
      , @RequestID         INT           = 0
      , @Data_Notification NVARCHAR(MAX) = N'';
    BEGIN TRY
        DECLARE @Xml XML = @Data;
        ---tao bien chua thong tin request------------------------------------------------------------------------------------
        DECLARE
            @StepNo     INT
          , @UserCode   NVARCHAR(100)
          , @TimeCreate DATETIME
          , @sender     VARCHAR(50)
          , @TypeId     INT = 15;
        IF @Xml IS NOT NULL
           AND @Xml.exist('*') <> 0
        BEGIN
            ---lay du lieu tu xml-----------------------------------------------------------------------------
            SELECT
                T.C.value('(UserName/text())[1]', 'nvarchar(100)') AS Username
              , T.C.value('(RequestId/text())[1]', 'int') AS RequestId
              , T.C.value('(Type/text())[1]', 'int') AS Type
            INTO
                #tmp_Request_15
            FROM
                @Xml.nodes('(Data)') T(C);

            SELECT TOP 1
                   @RequestID = R.RequestId
                 , @UserCode = R.Username
            FROM
                #tmp_Request_15 R;
            ---Neu co du lieu request-----------------------------------------------------------------------------------------------
            IF EXISTS (
                          SELECT TOP 1
                                 1
                          FROM
                              dbo.Requests ( NOLOCK )
                          WHERE
                              Id = @RequestID
                              AND TypeId = @TypeId
                              AND Status < 4
                      )
            BEGIN

                SELECT TOP 1
                       @StepNo = R.StepNo
                     , @TimeCreate = R.TimeCreate
                     , @sender = R.Sender
                FROM
                    dbo.Requests R ( NOLOCK )
                WHERE
                    Id = @RequestID;

                ---CC----------------------------------------------------------------------------------------
                SELECT
                    T.C.value('(text())[1]', 'varchar(20)') AS Cc
                INTO
                    #tmp_CC
                FROM
                    @Xml.nodes('(Data/Cc/row)') T(C);

                DELETE FROM #tmp_CC WHERE ISNULL(Cc, '') = '';

                DELETE
                #tmp_CC
                FROM
                    #tmp_CC a
                    LEFT JOIN dbo.F03_Employees b ( NOLOCK ) ON a.Cc = b.EmployeeCode
                                                                AND b.Status = 'A'
                WHERE
                    b.EmployeeCode IS NULL;

                DELETE
                #tmp_CC
                FROM
                    #tmp_CC AS C
                    INNER JOIN dbo.Assigners AS A ON A.EmployeeCode = C.Cc
                WHERE
                    A.RequestId = @RequestID
                    AND A.StepNo = @StepNo
                    AND A.Status = 1;

                ---Files------------------------------------------------------------------------------------
                SELECT
                    T.C.value('(text())[1]', 'nvarchar(1000)') AS FileName
                INTO
                    #tmp_Files
                FROM
                    @Xml.nodes('(Data/FileNames/FileName)') T(C);

                ---Insert thanh cong - insert thong tin lien quan ------------------------------------------------------------------------------------
                IF EXISTS ( SELECT TOP 1 1 FROM #tmp_Request_15 )
                BEGIN
                    ---Detail------------------------------------------------------------------------------------
                    SELECT
                        Tbl.Col.value('Id[1]', 'BIGINT') AS Id
                      , Tbl.Col.value('HuongXuLy_Code[1]', 'nvarchar(50)') AS HuongXuLy_Code
                      , Tbl.Col.value('SOEcom_new[1]', 'nvarchar(50)') AS SOEcom_New
                      , Tbl.Col.value('SOPos_new[1]', 'nvarchar(50)') AS SOPos_new
                    INTO
                        #tmp_details
                    FROM
                        @Xml.nodes('Data/DetailsData/row') Tbl(Col);


                    IF EXISTS ( SELECT TOP 1 1 FROM #tmp_details )
                    BEGIN
                        UPDATE
                            R
                        SET
                            R.Property4 = D.HuongXuLy_Code
                          , R.Property5 = D.SOEcom_New
                          , R.Property6 = D.SOPos_new
                        FROM
                            #tmp_details AS D
                            INNER JOIN dbo.RequestDetails R ( NOLOCK ) ON R.Id = D.Id
                        WHERE
                            R.RequestId = @RequestID;
                    END;

                    ---tao file đính kèm neu co -------------------------------------------------------------------
                    IF EXISTS ( SELECT TOP 1 1 FROM #tmp_Files )
                    BEGIN

                        INSERT INTO dbo.FileAttachs
                        (
                            RequestId
                          , RequestDetailId
                          , StepNo
                          , Uri
                          , TimeCreate
                          , CreateBy
                          , Status
                          , ApproveStatus
                          , Domain
                        )
                        SELECT
                            @RequestID
                          , NULL
                          , @StepNo
                          , a.FileName
                          , GETDATE()
                          , @UserCode
                          , 1
                          , 0
                          , NULL
                        FROM
                            #tmp_Files a;

                    END;

                    ---CC ------------------------------------------------------------------------------------------------
                    IF EXISTS ( SELECT TOP 1 1 FROM #tmp_CC )
                    BEGIN
                        INSERT INTO dbo.Assigners
                        (
                            RequestId
                          , EmployeeCode
                          , StepNo
                          , Type
                          , Status
                          , TimeCreate
                          , GroupMail
                          , IsRead
                          , Requests_ARCH_Id
                          , TypeId
                        )
                        SELECT DISTINCT
                               @RequestID
                             , a.Cc
                             , 1
                             , 2
                             --1 nguoi xu ly , 2 cc
                             , 1
                             -- cho xu ly
                             , GETDATE()
                             , NULL
                             , 0
                             , NULL
                             , 1
                        FROM
                            #tmp_CC a;
                    END;

                    -- update Calllog------------------------------------------------------------------

                    DECLARE @NextStep INT = @StepNo + 1;

                    UPDATE
                        dbo.Requests
                    SET
                        StepNo = @NextStep
                      , StepStatus = 2
                      , Status = 2
                    WHERE
                        Id = @RequestID;


                    --SELECT @NextStep
                    EXEC dbo.Assigners_InsertForRequest
                        @RequestId = @RequestID
                      -- bigint
                      , @StepNo = @NextStep
                      -- int
                      , @Is_NotView = 1; -- bit

                    EXEC dbo.RequestStep_Insert
                        @RequestId = @RequestID -- bigint
                      , @StepNo = @StepNo -- int
                      , @User = @UserCode; -- varchar(50)

                    EXEC dbo.RequestStep_Insert
                        @RequestId = @RequestID -- bigint
                      , @StepNo = @NextStep -- int
                      , @User = NULL; -- varchar(50)

                    ---insert bang gui mail---------------------------------------------------------------------------------
                    EXEC dbo.Email_MakeByProcess
                        @RequestId = @RequestID -- bigint
                      , @Title = N'' -- nvarchar(300)
                      , @TypeProcess = 5; -- int

                    ---tra ve thong bao
                    SET @Message = N'Chuyển bước Calllog ' + CONVERT(VARCHAR(20), @RequestID) + N' thành công! <br/>';

                    -- Gửi Notification + SMS
                    BEGIN
                        DECLARE
                            @json_sms         NVARCHAR(MAX)  = N''
                          , @SDTKH            VARCHAR(50)    = ''
                          , @SO_Ecom          VARCHAR(50)    = ''
                          , @SoTienCanPhaiTra NUMERIC(19, 0) = 0
                          , @Title            NVARCHAR(500)  = N''
                          , @Body             NVARCHAR(500)  = N'';

                        SELECT TOP 1
                               @SDTKH = RD.Property7
                             , @SO_Ecom = RD.Property1
                             , @SoTienCanPhaiTra = RD.Money3
                        FROM
                            dbo.RequestDetails AS RD ( NOLOCK )
                        WHERE
                            RD.RequestId = @RequestID;

                        SET @Title = CONCAT(N'Bạn vừa Hủy/Trả đơn hàng ', @SO_Ecom);
                        SET @Body = CONCAT(N'Nhà thuốc FPT Long Châu đã xử lý hoàn tiền cho Quý khách số tiền ', FORMAT(@SoTienCanPhaiTra, 'N0'), N'đ. Vui lòng kiểm tra số dư tài khoản với thẻ ATM từ 5-7 ngày, thẻ tín dụng vào kỳ sao kê gần nhất hoặc tối đa 45 ngày theo quy định ngân hàng.');

                        SELECT
                            @Data_Notification = N'{  "usercode": "{user}",
  "fromSys": "Calllog",
  "to": [
    "{cusphone}"
  ],
  "sendTime": "",
  "notification": {
    "title": "{title}",
    "body": "{body}",
    "url": "",
    "icon": "ORDER_RETURN",
    "sound": "default",
    "type": "ORDER"
  },
  "data": {
    "title": "",
    "body": "",
    "type": "DETAIL_ORDER",
    "typeOrder": "RETURN",
    "idEvent": "{ecomnum}",
    "flagButton": false,
    "buttonName": ""
  }
}'                      ;
                        --SET @SDTKH = '0352483751';
                        --SET @Title = N'test DB';
                        --SET @Body = N'testDB';

                        SET @Data_Notification = REPLACE(@Data_Notification, '{user}', @UserCode);
                        SET @Data_Notification = REPLACE(@Data_Notification, '{cusphone}', @SDTKH);
                        SET @Data_Notification = REPLACE(@Data_Notification, '{title}', @Title);
                        SET @Data_Notification = REPLACE(@Data_Notification, '{body}', @Body);
                        SET @Data_Notification = REPLACE(@Data_Notification, '{ecomnum}', @SO_Ecom);

                        INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
                        VALUES (
                                   @StoreName -- Title - nvarchar(300)
                                 , CONCAT(N'--Push notification: body', @Data_Notification) -- Error - nvarchar(max)
                                 , 0 -- Status - tinyint
                                 , GETDATE() -- TimeCreate - datetime
                               );

                        SET @json_sms = CONCAT(N'{"docNum": 0,"system":"CallLog","userID":"', @UserCode, '","cardCode":"","cardName":"", "sendTime":"","address": "', @SDTKH, '","templateId": "111","values": {"MA_DH": "', @SO_Ecom, '","SO_TIEN": "', FORMAT(@SoTienCanPhaiTra, 'N0'), '"}}');
                        INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
                        VALUES (
                                   @StoreName -- Title - nvarchar(300)
                                 , CONCAT(N'--Push sms: EXEC MPOS.dbo.push_notification_service @json_input=', @json_sms) -- Error - nvarchar(max)
                                 , 0 -- Status - tinyint
                                 , GETDATE() -- TimeCreate - datetime
                               );

                        EXEC MPOS.dbo.push_notification_service @json_input = @json_sms; -- nvarchar(max)
                    END;

                    SET @ResultID = 1;

                    DROP TABLE #tmp_details;
                END;
                ---Xoa bang------------------------------------------------------------------------------------
                DROP TABLE
                    #tmp_CC
                  , #tmp_Files;
            END;
            ELSE
            BEGIN
                SET @Message = N'Chuyển bước tất bại<br/> Không tìm thấy thông tin calllog';
            END;

            ---Xoa bang------------------------------------------------------------------------------------
            DROP TABLE #tmp_Request_15;

        END;
        ELSE
        BEGIN
            SET @Message = N'Chuyển bước thất bại<br/> Không lấy trích được thông tin calllog';
        END;
    END TRY
    BEGIN CATCH
        SET @ResultID = 0;
        SET @Message = N'Lỗi catch !';
        DECLARE @log_catch NVARCHAR(MAX) = CONCAT(@log, '- ,ERROR_MESSAGE: ', ERROR_MESSAGE(), ' ,ERROR_LINE: ', ERROR_LINE());
        INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
        VALUES (
                   @StoreName -- Title - nvarchar(300)
                 , @log_catch -- Error - nvarchar(max)
                 , 1 -- Status - tinyint
                 , GETDATE() -- TimeCreate - datetime
               );
    END CATCH;

    SELECT
        @ResultID AS ResultID
      , @Message AS Message
      , @RequestID AS ID
      , @Data_Notification AS Data_Notification;
END;
GO

