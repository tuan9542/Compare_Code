SET QUOTED_IDENTIFIER ON;
SET ANSI_NULLS ON;
GO
/*
==================================================
Author			:	TuanNA89
Create date		:	26/12/2018
Description		:	Calllog gọi store POS để check thông tin sản phẩm và Imei
Note			:	
==================================================
Test:
EXEC sp_CalllogHangDemo_CallPOS_CheckImei_New
  @RequestId = 0
, @TypeId = 0

*/
ALTER PROCEDURE dbo.sp_CalllogHangDemo_CallPOS_CheckImei_New ( @RequestId BIGINT = 0, @TypeId INT = 0, @User VARCHAR(20) = '' )
AS
    BEGIN
        SET XACT_ABORT ON;

        -- SELECT
        --     1 AS 'Result'
        --   , 'Thành công' AS 'Msg'
        --   , '123' AS 'SoYCDC';
        --RETURN

        DECLARE
            @Log NVARCHAR(MAX) = N''
          , @resultOut INT = 0
          , @msgOut NVARCHAR(500) = N''
          , @ycdcDocEntry INT = 0;

        SET @Log += N', @RequestId=''' + ISNULL(CONVERT(NVARCHAR(50), @RequestId), 'NULL') + N'''';
        SET @Log += N', @TypeId=''' + ISNULL(CONVERT(NVARCHAR(50), @TypeId), 'NULL') + N'''';
        SET @Log += N', @User=''' + ISNULL(CONVERT(NVARCHAR(50), @User), 'NULL') + N'''';

        BEGIN TRY
            DECLARE @ErrorMessage_Log NVARCHAR(MAX) = N'';
            SET @ErrorMessage_Log = N'Log: [sp_CalllogHangDemo_CallPOS_CheckImei_New]' + @Log + N' ! ';

            --	===Ghi Log===
            INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
            VALUES (
                       N'sp_CalllogHangDemo_CallPOS_CheckImei_New' -- Title - nvarchar(300)
                     , @ErrorMessage_Log -- Error - nvarchar(max)
                     , 0 -- Status - tinyint
                     , GETDATE() -- TimeCreate - datetime
                   );

            DECLARE
                @StatusRequest INT = 0
              , @ResultPOS INT = 0;

            SELECT TOP 1
                   @StatusRequest = Status
            FROM
                dbo.Requests ( NOLOCK )
            WHERE
                Id = @RequestId
                AND TypeId = @TypeId;

            IF ( ISNULL(@StatusRequest, 0) = 0 )
                BEGIN
                    SELECT
                        @resultOut = 0
                      , @msgOut = N'Yêu cầu không tồn tại. Xin kiểm tra lại'
                      , @ycdcDocEntry = 0;
                END;
            ELSE IF @StatusRequest = 4
                     BEGIN
                         SELECT
                             @resultOut = 0
                           , @msgOut = N'Yêu cầu đã được hoàn tất. Không thể thao tác!'
                           , @ycdcDocEntry = 0;
                     END;
            ELSE IF @StatusRequest = 5
                     BEGIN
                         SELECT
                             @resultOut = 0
                           , @msgOut = N'Yêu cầu đã bị hủy. Không thể thao tác!'
                           , @ycdcDocEntry = 0;
                     END;
            ELSE
                     BEGIN
                         UPDATE
                             dbo.RequestDetails
                         SET
                             Quantity = 1
                         WHERE
                             RequestId = @RequestId
                             AND ItemId = 65
                             AND Quantity = 0
                             AND Status = 1;

                         SELECT
                             ROW_NUMBER() OVER ( ORDER BY R.Id ) STT
                           , R.Id AS Id_RequestDetail
                           , R.RequestId
                           , R.ProductCode
                           , R.ProductName
                           , R.Property1 AS 'ImeiCode'
                           , R.Quantity
                           , R.Property5 AS 'StockOut'
                           , R.ShopCode AS 'WarehouseCode'
                           , R.SaleCode AS 'Id_ProductDemos'
                           , R.ItemId AS 'Subject_ProductDemos'
                         INTO
                             #tmp_RequestDetails
                         FROM
                             dbo.RequestDetails R ( NOLOCK )
                         WHERE
                             R.RequestId = @RequestId
                             AND R.ItemId <> 65
                             AND ISNULL(R.Quantity1, 0) <> 1
                             AND R.Status = 1; --Add - TuanNA89 - 29/07/2019 - chỉ lấy line còn hiệu lực

                         SELECT
                             WarehouseCode
                           , WarehouseCodeB1
                         INTO
                             #tmp_Warehouse
                         FROM
                             dbo.Warehouse ( NOLOCK )
                         WHERE
                             WarehouseCode IN ( SELECT WarehouseCode FROM #tmp_RequestDetails );

                         SELECT
                             WhsCode AS WhsCode
                           , U_WHS_TYPE AS U_WHS_TYPE
                           , U_Code_SH AS U_Code_SH
                         INTO
                             #tmp_OWHS
                         FROM
                             FRT_MDM.dbo.OWHS ( NOLOCK )
                         WHERE
                             U_Code_SH IN ( SELECT WarehouseCodeB1 FROM #tmp_Warehouse );

                         SELECT
                             D.STT
                           , D.Id_RequestDetail
                           , D.ProductName
                           , D.Id_ProductDemos
                           , D.ProductCode AS 'ItemCode'
                           , ImeiCode AS 'IMEI'
                           , Quantity AS 'Quantity'
                           , W.WarehouseCodeB1 AS 'Shop'
                           , O.WhsCode AS 'WhsCode'
                           , @User AS 'Createby'
                           , D.Subject_ProductDemos
                         INTO
                             #tmp_CheckImei_POS
                         FROM
                             #tmp_RequestDetails AS D
                             INNER JOIN #tmp_Warehouse AS W
                                 ON W.WarehouseCode = D.WarehouseCode
                             INNER JOIN #tmp_OWHS AS O
                                 ON O.U_Code_SH = WarehouseCodeB1
                                    AND O.U_WHS_TYPE = D.StockOut;

                         DECLARE
                             @xml_DetailDemo NVARCHAR(3000) = N''
                           , @Title_Email NVARCHAR(MAX) = N''
                           , @STT INT = 1
                           , @Id_RequestDetail BIGINT = 0
                           , @Id_ProductDemos BIGINT = 0
                           , @Subject_ProductDemos BIGINT = 0
                           , @Imei NVARCHAR(MAX) = N''
                           , @MaSP NVARCHAR(40) = N''
                           , @MsgPos NVARCHAR(MAX) = N'';

                         WHILE ( @STT <= ( SELECT MAX(STT)FROM #tmp_CheckImei_POS ))
                             BEGIN
                                 SELECT
                                     @xml_DetailDemo = N''
                                   , @Id_RequestDetail = 0
                                   , @Id_ProductDemos = 0
                                   , @Subject_ProductDemos = 0
                                   , @resultOut = 0
                                   , @Imei = N''
                                   , @MaSP = N''
                                   , @ycdcDocEntry = 0;

                                 SET @xml_DetailDemo = (
                                                           SELECT
                                                               ItemCode AS 'ItemCode'
                                                             , IMEI AS 'IMEI'
                                                             , Quantity AS 'Quantity'
                                                             , Shop AS 'Shop'
                                                             , WhsCode AS 'WhsCode'
                                                             , Createby AS 'Createby'
                                                           FROM
                                                               #tmp_CheckImei_POS
                                                           WHERE
                                                               STT = @STT
                                                           FOR XML PATH('LineItem'), ROOT('DATA')
                                                       );

                                 SELECT TOP 1
                                        @Subject_ProductDemos = Subject_ProductDemos
                                      , @Id_RequestDetail = Id_RequestDetail
                                      , @Id_ProductDemos = Id_ProductDemos
                                      , @Imei = IMEI
                                      , @MaSP = ItemCode
                                 FROM
                                     #tmp_CheckImei_POS
                                 WHERE
                                     STT = @STT;

                                 PRINT @xml_DetailDemo;
                                 PRINT CONCAT('', @Subject_ProductDemos);

                                 BEGIN
                                     EXEC POS_HNI.dbo.sp_POS_CLG_Demo_ExIm
                                         @CallLogID = @RequestId
                                       -- int
                                       , @DetailDemoXML = @xml_DetailDemo
                                       -- nvarchar(max)  --- Cần Mã sản phẩm, Imei, Số lượng, Mã Shop, Mã kho, Người xử lý
                                       , @TypeCallLog = @Subject_ProductDemos
                                       -- int ---0 Xuất demo --- 1 Bỏ mẫu
                                       , @ResultOut = @resultOut OUT
                                       -- int  --- 0 Là Có lỗi, 1 là Thành công
                                       , @MsgOut = @msgOut OUT
                                       -- nvarchar(500) --- Mô tả
                                       , @YCDCDocEntry = @ycdcDocEntry OUT; -- int -- Số chứng từ YCDC

                                     UPDATE
                                         dbo.RequestDetails
                                     SET
                                         Quantity1 = @resultOut
                                       , Property11 = @msgOut
                                       , Property12 = @ycdcDocEntry
                                       , Time1 = GETDATE()
                                     WHERE
                                         Id = @Id_RequestDetail
                                         AND RequestId = @RequestId;

                                     SET @MsgPos = @MsgPos + CASE WHEN ISNULL(@msgOut, '') = '' THEN '' ELSE '<br>' END + N'-Thông tin kiểm tra sản phẩm ' + @MaSP + N' và imei ' + @Imei + N' từ Hệ thống POS: ' + CONVERT(VARCHAR(10), @resultOut) + N'-' + @msgOut;
                                 END;
                                 SET @STT += 1;
                             END;

                         INSERT INTO dbo.Conversations
                         (
                             RequestId
                           , StepNo
                           , Sender
                           , Message
                           , CreateBy
                           , Type
                           , Status
                           , RequestDetailId
                           , TimeCreate
                           , Requests_ARCH_Id
                         )
                         VALUES (
                                    @RequestId -- RequestId - bigint
                                  , 1 -- StepNo - int
                                  , N'-1' -- Sender - nvarchar(40)
                                  , @MsgPos -- Message - nvarchar(max)
                                  , N'-1' -- CreateBy - nvarchar(max)
                                  , 2 -- Type - tinyint
                                  , 1 -- Status - tinyint
                                  , NULL -- RequestDetailId - bigint
                                  , GETDATE() -- TimeCreate - datetime
                                  , NULL -- Requests_ARCH_Id - bigint
                                );

                         SELECT
                             Id
                           , SaleCode
                           , Quantity1
                         INTO
                             #tmp_Check_HoanTat
                         FROM
                             dbo.RequestDetails ( NOLOCK )
                         WHERE
                             RequestId = @RequestId
                             AND ISNULL(Quantity1, 0) <> 1
                             AND Status = 1; --Add - TuanNA89 - 29/07/2019 - chỉ lấy line còn hiệu lực

                         IF ( EXISTS ( SELECT 1 FROM #tmp_Check_HoanTat ))
                             BEGIN
                                 SELECT
                                     @resultOut = 0
                                   , @msgOut = N'Yêu cầu còn sản phẩm chưa tạo được YCDC bên POS. Xin kiểm trả lại tồn kho';
                             END;
                         ELSE
                             BEGIN
                                 UPDATE
                                     dbo.Requests
                                 SET
                                     Status = 4
                                   , TimeFinish = GETDATE()
                                   , TimeClose = GETDATE()
                                   , Remark = CONCAT(Remark, N' - (', CONVERT(NVARCHAR(50), GETDATE(), 121), N'): ', N'sp_CalllogHangDemo_CallPOS_CheckImei_New-UPDATE-Auto hoàn tất do POS check Imei thành công!')
                                   , TimeAppear = CASE WHEN TimeAppear IS NULL THEN GETDATE()ELSE TimeAppear END
                                 WHERE
                                     Id = @RequestId
                                     AND TypeId = @TypeId;

                                 SELECT @resultOut = 1, @msgOut = N'Hoàn tất Calllog';

                                 EXEC dbo.CompletedProductDemo_210
                                     @requestId = @RequestId
                                   -- bigint
                                   , @updateBy = @User; -- nvarchar(100)

                                 SELECT TOP 1
                                        @Title_Email = N'[CallLog] - ' + R.Title
                                 FROM
                                     dbo.Requests R ( NOLOCK )
                                 WHERE
                                     R.Id = @RequestId;

                                 EXEC dbo.Email_MakeByProcess
                                     @RequestId = @RequestId
                                   , @Title = @Title_Email
                                   , @TypeProcess = 4;

                                 -- Add - TuanNA89 01/06/2019 -- Xóa Calllog trên bảng tạm của mobile
                                 EXEC dbo.sp_Requests_Temporary_Delete @RequestID = @RequestId; -- bigint

                             END;

                         IF @resultOut <> 1
                             SET @resultOut = 0;

                         DROP TABLE
                             #tmp_CheckImei_POS
                           , #tmp_Check_HoanTat
                           , #tmp_OWHS
                           , #tmp_RequestDetails
                           , #tmp_Warehouse;
                     END;
        END TRY
        BEGIN CATCH
            DECLARE @ErrorMessage NVARCHAR(MAX) = N'';
            SET @ErrorMessage = N'Lỗi: [sp_CalllogHangDemo_CallPOS_CheckImei_New]' + @Log;
            SET @ErrorMessage += N' - ERROR_MESSAGE: ' + ERROR_MESSAGE() + N' - ERROR_LINE: ' + CONVERT(NVARCHAR, ERROR_LINE());

            --	===Ghi Log===
            INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
            VALUES (
                       N'sp_CalllogHangDemo_CallPOS_CheckImei_New' -- Title - nvarchar(300)
                     , @ErrorMessage -- Error - nvarchar(max)
                     , 1 -- Status - tinyint
                     , GETDATE() -- TimeCreate - datetime
                   );
        END CATCH;

        SELECT @resultOut AS 'Result', @msgOut AS 'Msg', @ycdcDocEntry AS 'SoYCDC';
    END;

----SELECT * FROM dbo.Logs(NOLOCK) WHERE Title=N'sp_CalllogHangDemo_CallPOS_CheckImei_New' ORDER BY Id DESC
GO

