SET QUOTED_IDENTIFIER ON;
SET ANSI_NULLS ON;
GO
ALTER PROC dbo.sp_CALLLOGPHAR_WEB_CreateCallLog_15 ( @Xmldata NVARCHAR(MAX) = '' )
AS
BEGIN
    DECLARE
        @StoreName    VARCHAR(50)   = OBJECT_NAME(@@PROCID)
      , @ErrorMessage NVARCHAR(MAX) = N'';
    SET @ErrorMessage = CONCAT(@StoreName, ' @Xmldata=N''', @Xmldata, '''');
    INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
    VALUES (
               @StoreName -- Title - nvarchar(300)
             , @ErrorMessage -- Error - nvarchar(max)
             , 0 -- Status - tinyint
             , GETDATE() -- TimeCreate - datetime
           );
    DECLARE
        @Out_Result    INT           = 0
      , @Out_Msg       NVARCHAR(500) = N'Thất bại'
      , @Out_RequestId BIGINT        = 0;
    BEGIN TRY

        DECLARE @Xml_Data XML = @Xmldata;

        SELECT
            Tbl.Col.value('SOEcom[1]', 'nvarchar(max)') AS SOEcom
          , Tbl.Col.value('SOPOS[1]', 'nvarchar(max)') AS SOPOS
          , Tbl.Col.value('GiaTriDonHang[1]', 'nvarchar(max)') AS GiaTriDonHang
          , Tbl.Col.value('GiaTriThanhToanQuaCongAlepay[1]', 'nvarchar(max)') AS GiaTriThanhToanQuaCongAlepay
          , Tbl.Col.value('LyDoHuy[1]', 'nvarchar(max)') AS LyDoHuy
          , Tbl.Col.value('SoTienCanPhaiTra[1]', 'nvarchar(max)') AS SoTienCanPhaiTra
          , Tbl.Col.value('SDTKH[1]', 'nvarchar(max)') AS SDTKH
        INTO
            #tmp_Details
        FROM
            @Xml_Data.nodes('/CreatePharmacy_15_Req/Data/row') Tbl(Col);
        IF EXISTS ( SELECT TOP 1 1 FROM #tmp_Details AS TD ( NOLOCK ))
        BEGIN

            SELECT
                Tbl.Col.value('NguoiTao[1]', 'varchar(50)') AS NguoiTao
              , Tbl.Col.value('NoiTao[1]', 'nvarchar(50)') AS NoiTao
              , Tbl.Col.value('TieuDe[1]', 'nvarchar(500)') AS TieuDe
              , Tbl.Col.value('MaShopB1[1]', 'varchar(50)') AS MaShopB1
              , Tbl.Col.value('Type[1]', 'int') AS Type -- 1: hủy đơn hàng, 2: trả hàng
              , Tbl.Col.value('System[1]', 'varchar(50)') AS System
            INTO
                #Tmp_Info
            FROM
                @Xml_Data.nodes('/CreatePharmacy_15_Req') Tbl(Col);

            DECLARE
                @NguoiTao VARCHAR(50)   = ''
              , @NoiTao   VARCHAR(50)   = ''
              , @MaShopB1 VARCHAR(50)   = ''
              , @TieuDe   NVARCHAR(500) = N''
              , @Type     INT           = 0
              , @System   VARCHAR(50)   = '';

            SELECT TOP 1
                   @NguoiTao = NguoiTao
                 , @NoiTao = NoiTao
                 , @MaShopB1 = MaShopB1
                 , @TieuDe = TieuDe
                 , @Type = Type
                 , @System = System
            FROM
                #Tmp_Info;

            DROP TABLE #Tmp_Info;
            DECLARE @typeId INT = 15; --Type Categories

            DECLARE @Ids AS TABLE ( Id BIGINT );

            INSERT INTO dbo.Requests
            (
                Sender
              , StepNo
              , Title
              , StepStatus
              , TimeCreate
              , TimeAppear
              , isHighlight
              , isParent
              , Status
              , CreateBy
              , TimeLastUpdate
              , UpdateBy
              , FromShop
              , FromOffice
              , ToShop
              , ToOffice
              , GroupId
              , Remark
              , Assigner
              , TypeId
              , TimeFutureFinish
              , FromSystem
            )
            OUTPUT
                Inserted.Id
            INTO @Ids
            SELECT
                @NguoiTao
              , 0 AS StepNo
              , @TieuDe
              , 1 AS StepStatus --cho xu ly
              , GETDATE() AS TimeCreate
              , GETDATE() AS TimeAppear
              , 0 AS isHighlight
              , 0 AS isParent
              , 0 AS Status --UnAvaliable
              , @NguoiTao AS CreateBy
              , GETDATE() AS TimeLastUpdate
              , @NguoiTao AS UpdateBy
              , NULL
              , NULL
              , @MaShopB1
              , NULL
              , 0
              , N'Có lỗi xảy ra - tạo yêu cầu thất bại'
              , NULL
              , @typeId
              , NULL
              , @System;

            DECLARE @RequestId BIGINT = 0;
            SELECT TOP 1 @RequestId = Id FROM @Ids;

            IF ( @RequestId > 0 )
            BEGIN
                INSERT INTO dbo.RequestDetails
                (
                    RequestId
                  , Status
                  , Property1
                  , Property2
                  , Property3
                  , Property4
                  , Property7
                  , Money1
                  , Money2
                  , Money3
                  , ItemId
                )
                SELECT
                    @RequestId
                  , 1
                  , D.SOEcom
                  , D.SOPOS
                  , D.LyDoHuy
                  , CASE WHEN @Type = 2 THEN 'HXL_1' ELSE NULL END
                  , D.SDTKH
                  , D.GiaTriDonHang
                  , D.GiaTriThanhToanQuaCongAlepay
                  , D.SoTienCanPhaiTra
                  , @Type
                FROM
                    #tmp_Details AS D;

                DECLARE @StepNo INT = CASE WHEN @Type = 2 THEN 2 ELSE 1 END;

                UPDATE
                    dbo.Requests
                SET
                    StepNo = @StepNo
                  , Status = 1
                  , Remark = CONCAT(N'-(', FORMAT(GETDATE(), 'dd/MM/yyyy hh:mm:ss'), '): ', @StoreName)
                WHERE
                    Id = @RequestId;

                EXEC dbo.Assigners_InsertForRequest
                    @RequestId = @RequestId -- bigint
                  , @StepNo = @StepNo -- int
                  , @Is_NotView = 1; -- bit

                SELECT
                    @Out_Result = 1 -- result - int
                  , @Out_Msg = CONCAT(N'Tạo thành công yêu cầu số ', @RequestId) -- msg - nvarchar(500)
                  , @Out_RequestId = @RequestId; -- requestId - bigint

                EXEC dbo.Email_MakeByProcess
                    @RequestId = @RequestId -- bigint
                  , @Title = N'' -- nvarchar(300)
                  , @TypeProcess = 1; -- int

                IF @StepNo = 2
                BEGIN
                    INSERT INTO dbo.RequestSteps ( RequestId, StepNo, TimeStart, TimeEnd, Assigner, TimeCreate, Status )
                    VALUES (
                               @RequestId -- RequestId - bigint
                             , 1 -- StepNo - int
                             , GETDATE() -- TimeStart - datetime
                             , GETDATE() -- TimeEnd - datetime
                             , '-1' -- Assigner - varchar(40)
                             , GETDATE() -- TimeCreate - datetime
                             , 1 -- Status - tinyint
                           );

                    -- Gửi Notification + SMS
                    BEGIN
                        --DECLARE
                        --    @Out_Result1 INT
                        --  , @Out_Msg1    NVARCHAR(MAX);
                        --EXEC master.dbo.sp_FRTCallLogPharmacy_CALLLOGPHAR_WEB_SendNotification_15
                        --    @RequestId = @RequestId -- int
                        --  , @User = '-1' -- varchar(50)
                        --  , @Out_Result = @Out_Result1 OUTPUT -- int
                        --  , @Out_Msg = @Out_Msg1 OUTPUT; -- nvarchar(max)
                        --INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
                        --VALUES (
                        --           @StoreName -- Title - nvarchar(300)
                        --         , CONCAT(N'--Push notification: EXEC master.dbo.sp_FRTCallLogPharmacy_CALLLOGPHAR_WEB_SendNotification_15 @RequestID=', @RequestId, ',@UserCode=-1 --> Result: ', @Out_Result1, '-', @Out_Msg1) -- Error - nvarchar(max)
                        --         , 0 -- Status - tinyint
                        --         , GETDATE() -- TimeCreate - datetime
                        --       );
                        DECLARE
                            @result           INT
                          , @messages         NVARCHAR(500)
                          , @id_log           BIGINT
                          , @json_sms         NVARCHAR(MAX)  = N''
                          , @SDTKH            VARCHAR(50)    = ''
                          , @SO_Ecom          VARCHAR(50)    = ''
                          , @SoTienCanPhaiTra NUMERIC(19, 0) = 0;

                        SELECT TOP 1
                               @SDTKH = RD.Property7
                             , @SO_Ecom = RD.Property1
                             , @SoTienCanPhaiTra = RD.Money3
                        FROM
                            dbo.RequestDetails AS RD ( NOLOCK )
                        WHERE
                            RD.RequestId = @RequestId;

                        SET @json_sms = CONCAT(N'{"docNum": 0,"system":"CallLog","userID":"","cardCode":"","cardName":"", "sendTime":"","address": "', @SDTKH, '","templateId": "111","values": {"MA_DH": "', @SO_Ecom, '","SO_TIEN": "', FORMAT(@SoTienCanPhaiTra, 'N0'), '"}}');

                        EXEC SV_THUC_MPOS.MPOS.dbo.push_notification_service
                            @json_input = @json_sms; -- nvarchar(max)

                        INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
                        VALUES (
                                   @StoreName -- Title - nvarchar(300)
                                 , CONCAT(N'--Push sms: EXEC master.dbo.push_notification_service @json_input=', @json_sms, '--> Result: ', @result, '-', @messages, '-', @id_log) -- Error - nvarchar(max)
                                 , 0 -- Status - tinyint
                                 , GETDATE() -- TimeCreate - datetime
                               );
                    END;
                END;
            END;
        END;

        DROP TABLE #tmp_Details;
    END TRY
    BEGIN CATCH
        INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
        VALUES (
                   @StoreName -- Title - nvarchar(300)
                 , CONCAT(@ErrorMessage, ' - ', ERROR_MESSAGE()) -- Error - nvarchar(max)
                 , 1 -- Status - tinyint
                 , GETDATE() -- TimeCreate - datetime
               );
    END CATCH;
    SELECT @Out_Result AS result, @Out_Msg AS msg, @Out_RequestId AS requestId;

END;




GO

