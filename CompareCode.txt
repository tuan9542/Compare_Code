SET QUOTED_IDENTIFIER ON;
SET ANSI_NULLS ON;
GO
/*
==================================================
Author			:	TuanNA89
Create date		:	17/04/2020
Description		:	Tạo Header CL Hợp đồng FEC lỗi
Note			:	
==================================================
*/
ALTER PROCEDURE CLHopDongFECLoi_Type227_Insert_Header
    @RequestId_Out BIGINT = 0 OUT
  , @Result_Out INT = 0 OUT
  , @Msg_Out NVARCHAR(500) = '' OUT
  , @Users_XuLy NVARCHAR(500) = '' -- 2222,1111
  , @MaShop VARCHAR(50) = ''
  , @NhaTraGop VARCHAR(20) = ''
AS
    BEGIN
        DECLARE
            @StoreName VARCHAR(50) = OBJECT_NAME(@@PROCID)
          , @ErrorMessage NVARCHAR(MAX) = N'';
        BEGIN TRY
            SET @ErrorMessage = N'[' + @StoreName + N']';
            SET @ErrorMessage = CONCAT(@ErrorMessage, N' @Users_XuLy=N''', ISNULL(@Users_XuLy, NULL), '''');
            SET @ErrorMessage = CONCAT(@ErrorMessage, N', @MaShop=N''', ISNULL(@MaShop, NULL), '''');
            SET @ErrorMessage = CONCAT(@ErrorMessage, N', @NhaTraGop=N''', ISNULL(@NhaTraGop, NULL), '''');

            INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
            VALUES (
                       @StoreName -- Title - nvarchar(300)
                     , @ErrorMessage -- Error - nvarchar(max)
                     , 0 -- Status - tinyint
                     , GETDATE() -- TimeCreate - datetime
                   );

            DECLARE
                @Title NVARCHAR(2000) = N''
              , @Content NVARCHAR(MAX) = N''
              , @TenShop NVARCHAR(255) = N''
              , @MaShop_Inside VARCHAR(50) = ''
              , @MaKhuVuc INT = 0
              , @TenKhuVuc NVARCHAR(500) = N''
              , @TimeCreate DATETIME = GETDATE()
              , @DiaChiShop NVARCHAR(500) = N''
              , @TenNhaTraGop NVARCHAR(500) = N'';

            SELECT TOP 1
                   @TenShop = WarehouseName
                 , @MaShop_Inside = WarehouseCode
                 , @MaKhuVuc = RegionL4
                 , @DiaChiShop = Address
            FROM
                dbo.Warehouse ( NOLOCK )
            WHERE
                WarehouseCodeB1 = @MaShop;

            SELECT TOP 1
                   @TenKhuVuc = RegionHierachyName
            FROM
                dbo.F03_RegionHierachies ( NOLOCK )
            WHERE
                RegionHierachyCode = @MaKhuVuc;

            IF @NhaTraGop IN ( '', 'FEC' )
                BEGIN
                    SET @TenNhaTraGop = N'FEC';
                END;

            SET @Title = CONCAT(N'Hợp đông trả góp ', @TenNhaTraGop, N' lỗi - ', @TenKhuVuc, N' - ', @TenShop, N' - ', FORMAT(@TimeCreate, 'dd/MM/yyyy'));
            SET @Content += N'Hợp đông số <span style="color:red">{SoHopDong}</span> của nhân viên {MaNVLamHS} - {TenNVLamHS} phát sinh <span style="color:red">{ChiTietLoi}</span>. Mời Anh/Chị bổ sung đầy đủ chứng từ theo như quy trình đã ban hành .<br>';
            SET @Content += CONCAT(N'Thời hạn cuối cùng để cập nhập là <span style="color:red">', FORMAT(DATEADD(DAY, 1, @TimeCreate), 'dd/MM/yyyy hh:mm:ss'), N'</span>. Sau thời gian này nếu hợp đồng chưa được giải ngân, QA sẽ truy thu nhân viên vi phạm.<br>');
            SET @Content += N'<span style="color:red">Hệ thống sẽ auto hoàn tất calllog khi hợp đồng được giải ngân</span>';

            DECLARE @RequestId BIGINT = 0;
            EXEC dbo.Request_InsertNoSelect
                @RequestID = @RequestId OUTPUT -- bigint
              , @Sender = '-1' -- varchar(40)
              , @Assigner = @Users_XuLy -- varchar(3000)
              , @Title = @Title -- nvarchar(300)
              , @Content = @Content -- nvarchar(max)
              , @TypeId = 227 -- int
              , @StepNo = 1 -- int
              , @ToShop = @MaShop_Inside -- varchar(40)
              , @Status = 1 -- tinyint
              , @CreateBy = '-1' -- varchar(10)
              , @SaleCode = @NhaTraGop;

            IF ( @RequestId > 0 )
                BEGIN
                    UPDATE
                        dbo.Requests
                    SET
                        Remark += CONCAT(@StoreName, N' Create! ')
                    WHERE
                        Id = @RequestId;

                    EXEC dbo.Email_MakeByProcess
                        @RequestId = @RequestId -- bigint
                      , @Title = N'' -- nvarchar(300)
                      , @TypeProcess = 1; -- int


                    SELECT
                        @RequestId_Out = @RequestId
                      , @Result_Out = 1
                      , @Msg_Out = N'Tạo CallLog thành công!';
                END;
        END TRY
        BEGIN CATCH
            SET @ErrorMessage += CONCAT(N' - ERROR_MESSAGE: ', ERROR_MESSAGE(), ' - ERROR_LINE: ', ERROR_LINE());

            --	===Ghi Log===
            INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
            VALUES (
                       @StoreName -- Title - nvarchar(300)
                     , CONCAT(N'Lỗi ', @ErrorMessage) -- Error - nvarchar(max)
                     , 1 -- Status - tinyint
                     , GETDATE() -- TimeCreate - datetime
                   );

            SELECT @Msg_Out = ERROR_MESSAGE();
        END CATCH;
    END;
GO

