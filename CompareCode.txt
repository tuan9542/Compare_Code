SET QUOTED_IDENTIFIER ON;
SET ANSI_NULLS ON;
GO
/*
==================================================
Author			:	TuanNA89
Create date		:	22/07/2020
Description		:	Xử lý callLog 229
Note			:	
==================================================
*/
ALTER PROCEDURE CallLogPOSM_DuyetHinhAnh_Type229_XuLy @RequestId BIGINT = 0
AS
BEGIN
    DECLARE
        @StoreName    VARCHAR(50)   = OBJECT_NAME(@@PROCID)
      , @ErrorMessage NVARCHAR(MAX) = N'';
    BEGIN TRY
        SET @ErrorMessage = N'[' + @StoreName + N']';
        SET @ErrorMessage = CONCAT(@ErrorMessage, N' @RequestId =N''', ISNULL(@RequestId, NULL), '''');


        INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
        VALUES (
                   @StoreName -- Title - nvarchar(300)
                 , @ErrorMessage -- Error - nvarchar(max)
                 , 0 -- Status - tinyint
                 , GETDATE() -- TimeCreate - datetime
               );

        DECLARE @StepNo INT = 0;
        DECLARE
            @p_Notify_EmpCode_String VARCHAR(300)  = ''
          , @p_Notify_Title          NVARCHAR(500) = N'Công việc cần thực hiện'
          , @p_Notify_NoiDung        NVARCHAR(500) = N'';
        SELECT
            @StepNo = R.StepNo
        FROM
            dbo.Requests AS R ( NOLOCK )
        WHERE
            R.Id = @RequestId;

        SELECT
            RD.RequestId
          , RD.Id
          , RD.Approved
          , RD.Property1 AS TenHangMuc
          , RD.Property7 AS LinkHinh
          , RD.Property8 AS LyDoKhongDuyet
        INTO
            #tmp_Details
        FROM
            dbo.RequestDetails AS RD ( NOLOCK )
        WHERE
            RD.RequestId = @RequestId
            AND RD.Approved = 0
            AND ISNULL(RD.Property7, '') <> '';


        UPDATE
            dbo.RequestDetails
        SET
            Property7 = ''
          , Property11 = ''
        WHERE
            RequestId = @RequestId
            AND Approved = 0;

        UPDATE
            F
        SET
            F.Status = 0
        FROM
            #tmp_Details AS D
            INNER JOIN dbo.FileAttachs AS F ON F.RequestId = D.RequestId
                                               AND F.RequestDetailId = D.Id
        WHERE
            D.Approved = 0
            AND F.Status = 1;

        IF ( @StepNo = 3 )
        BEGIN
            EXEC dbo.Assigners_InsertForRequest
                @RequestId = @RequestId -- bigint
              , @StepNo = @StepNo -- int
              , @Is_NotView = 1; -- bit

            SELECT
                @p_Notify_EmpCode_String = ''
              , @p_Notify_Title = N'Công việc cần thực hiện'
              , @p_Notify_NoiDung = CONCAT(N'Calllog Hình ảnh triển khai POSM ', @RequestId, N' bị từ chối hình ảnh, bạn vui lòng kiểm tra lại và xử lý theo thông tin được gửi về.');

            SELECT
                @p_Notify_EmpCode_String = COALESCE(@p_Notify_EmpCode_String, '') + EmployeeCode + ','
            FROM
                Assigners ( NOLOCK )
            WHERE
                RequestId = @RequestId
                AND StepNo = @StepNo
                AND Type = 1
                AND Status = 1;

            EXEC MPOS.dbo.FRT_SP_InsertNotify_oneapp
                @UserID = @p_Notify_EmpCode_String
              , @Title = @p_Notify_Title
              , @NoiDung = @p_Notify_NoiDung
              , @sys_name = N'mCallLog'
              , @is_level = 2 -- * Mức độ: 2:Chú ý
              , @type = 0 -- int
              , @URL = N'' -- nvarchar(500)
              , @CallLog_ReqId = @RequestId -- int
              , @CallLog_TypeId = 229 -- int
              , @url_picture = N''; -- nvarchar(200)

            DECLARE
                @Request_StepNo_Current         INT = 0
              , @Request_StepNo_Current_TimeEnd DATETIME;
            SELECT TOP 1
                   @Request_StepNo_Current = RS.StepNo
            FROM
                dbo.RequestSteps AS RS ( NOLOCK )
            WHERE
                RS.RequestId = @RequestId
                AND RS.StepNo IN (2, 4)
            ORDER BY
                RS.StepNo DESC;

            DECLARE @Conversation NVARCHAR(MAX) = N'';
            SELECT
                @Conversation = STRING_AGG(CONCAT(TD.TenHangMuc, ' - ', N'Lý do từ chối: ', TD.LyDoKhongDuyet, ' - Link hình: <a href="', TD.LinkHinh, '" target="_blank">', TD.LinkHinh, '</a>'), '<br>')
            FROM
                #tmp_Details AS TD ( NOLOCK );

            INSERT INTO dbo.Conversations
            (
                RequestId
              , StepNo
              , Sender
              , Message
              , CreateBy
              , Type
              , Status
              , RequestDetailId
              , TimeCreate
              , Requests_ARCH_Id
              , IPClient
              , FromForm
            )
            SELECT
                @RequestId
              , @StepNo
              , '-1'
              , @Conversation
              , '-1'
              , 2
              , 1
              , NULL
              , GETDATE()
              , NULL
              , NULL
              , NULL;

            SELECT 0 Result, N'Chuyển bước thành công' Message;
        END;
        ELSE IF ( @StepNo = 4 )
        BEGIN
            UPDATE dbo.Requests SET StepNo = 3, Status = 2 WHERE Id = @RequestId;

            SELECT
                @p_Notify_EmpCode_String = ''
              , @p_Notify_Title = N'Công việc cần thực hiện'
              , @p_Notify_NoiDung = CONCAT(N'Calllog Hình ảnh triển khai POSM ', @RequestId, N' bị từ chối duyệt vì có hình ảnh không đạt yêu cầu');

            SELECT
                @p_Notify_EmpCode_String = COALESCE(@p_Notify_EmpCode_String, '') + A.EmployeeCode + ','
            FROM
                dbo.Assigners AS A ( NOLOCK )
            WHERE
                RequestId = @RequestId
                AND StepNo = 3
                AND Type = 1
                AND Status = 1
            GROUP BY
                A.EmployeeCode;

            EXEC MPOS.dbo.FRT_SP_InsertNotify_oneapp
                @UserID = @p_Notify_EmpCode_String
              , @Title = @p_Notify_Title
              , @NoiDung = @p_Notify_NoiDung
              , @sys_name = N'mCallLog'
              , @is_level = 2 -- * Mức độ: 2:Chú ý
              , @type = 0 -- int
              , @URL = N'' -- nvarchar(500)
              , @CallLog_ReqId = @RequestId -- int
              , @CallLog_TypeId = 229 -- int
              , @url_picture = N''; -- nvarchar(200)

            INSERT INTO dbo.Conversations ( RequestId, StepNo, Sender, Message, CreateBy, TimeCreate, Type, Status )
            VALUES (
                       @RequestId -- RequestId - bigint
                     , @StepNo -- StepNo - int
                     , N'-1' -- Sender - nvarchar(40)
                     , N'Hệ thống auto cập nhật tình trạng duyệt công việc là Không duyệt vì trên calllog tồn tại hình ảnh không được duyệt' -- Message - nvarchar(max)
                     , N'-1' -- CreateBy - nvarchar(max)
                     , GETDATE()
                     , 2 -- Type - tinyint
                     , 1 -- Status - tinyint
                   );
            EXEC dbo.sp_Requests_Temporary_Delete @RequestID = @RequestId; -- bigint

            SELECT 1 Result, N'' Message;
        END;
    END TRY
    BEGIN CATCH
        SET @ErrorMessage += N' - ERROR_MESSAGE: ' + ERROR_MESSAGE() + N' - ERROR_LINE: ' + CONVERT(NVARCHAR, ERROR_LINE());
        PRINT @ErrorMessage;
        --	===Ghi Log===
        INSERT INTO dbo.Logs ( Title, Error, Status, TimeCreate )
        VALUES (
                   @StoreName -- Title - nvarchar(300)
                 , CONCAT(N'Lỗi ', @ErrorMessage) -- Error - nvarchar(max)
                 , 1 -- Status - tinyint
                 , GETDATE() -- TimeCreate - datetime
               );

    END CATCH;
END;

----SELECT * FROM dbo.Logs(NOLOCK) WHERE Title=N'CallLog_POSMHinhAnh_Type_Create' AND CHARINDEX(N'', Error)>0 ORDER BY Id DESC

GO

